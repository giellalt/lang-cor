! Cornish noun morphology

! Masculine stem classes
! ======================
! The nouns get tags (to the left of :, i.e. in the upper representation
! and the suffixes are found to the left of the colon

LEXICON m 
+N+Sem/Msc: sg ; ! no suffix, of course
+N+Sem/Msc: owpl ; ! no suffix yet, see LEXICON owpl below

LEXICON m-yow
+N+Sem/Msc:    sg ;
+N+Sem/Msc:    yowpl ;

LEXICON m-ow
+N+Sem/Msc:    sg ;
+N+Sem/Msc:    owpl ;


LEXICON m-ys
+N+Sem/Msc:    sg ;
+N+Sem/Msc:    yspl ;

LEXICON m-es
+N+Sem/Msc:    sg ;
+N+Sem/Msc:    espl ;

LEXICON m-s
+N+Sem/Msc:    sg ;
+N+Sem/Msc:    spl ;

LEXICON m-yn
+N+Sem/Msc:    sg ;
+N+Sem/Msc:    ynpl ;

LEXICON m-ens
+N+Sem/Msc:    sg ;
+N+Sem/Msc:    enspl ;

LEXICON m-yon
+N+Sem/Msc:    sg ;
+N+Sem/Msc:    yonpl ;

!LEXICON m-aff-yon
!+N+Sem/Msc:    sg ;
!+N+Sem/Msc:    yonpl ;

LEXICON m-aff-i
+N+Sem/Msc:    sg  ;
+N+Sem/Msc:    ipl ; ! Need no trigger to signal MPh rule

LEXICON m-ag
+N+Sem/Msc:%^E  sg ; ! ditto
+N+Sem/Msc:     yonpl ;

LEXICON m-sgtantum
+N+Sem/Msc:  mutation ;

LEXICON m-pltantum
+N+Sem/Msc+PLT:   mutation ;

! Feminine stem classes
! =====================

LEXICON f
+N+Sem/Fem:  sg ;
+N+Sem/Fem:  owpl ;

LEXICON f-yow
+N+Sem/Fem:  sg ;
+N+Sem/Fem:  yowpl ;

LEXICON f-edh
+N+Sem/Fem:  sg ;
+N+Sem/Fem:  edhpl ;

!LEXICON faff
!+N+Sem/Fem:    sg ;
!+N+Sem/Fem:%^V yonpl ;


! Nest step: 
! Here we add suffixes. These may be common to several stem classes
! and are therefore pointed to in a different layer (this one)

! Number lexica
! =============

LEXICON sg
+Sg: mutation ; ! No suffix here, only tag.

LEXICON yonpl
+Pl:%>yon mutation ; ! Plural is yon, and there is mutation to come.
!+Pl:%^V%>yon mutation ; ! Plural is yon, and there is mutation to come.

LEXICON owpl
+Pl:%>ow mutation ;
!:>%>ek adjd ;

LEXICON yowpl
+Pl:%>yow mutation ;
:%>yek adjd ;

LEXICON ipl 
+Pl:%>i   mutation ;
:%>ek adjd ;

LEXICON yspl
+Pl:%>ys  mutation ;

LEXICON ynpl
+Pl:%>yn  mutation ;

LEXICON enspl
+Pl:%>ens  mutation ;
:%>ek adjd ;

LEXICON espl
+Pl:%>es  mutation ;
:%>ek adjd ;

LEXICON edhpl
+Pl:%>edh  mutation ;
:%>ek adjd ;

LEXICON spl
+Pl:%>s  mutation ;
:%>ek adjd ;

LEXICON adjd
+A: # ;

! Irregular plurals
!==================

LEXICON KI
+Sg:	ki # ;
+Pl:	keun # ;

LEXICON DEN
+Sg:	den #;
+Pl:	tus # ;

LEXICON OJYON
+Sg:	ojyon #;
+Pl:	ohen # ;

LEXICON GARLONT
+N+Sg:           mutation ; ! or #?
+N+Pl:%>%^STDELs mutation ; ! or #?

! Mutation lexicon
! ================

! This is the final lexicon. Here, no suffix is added, only some marks.
! they will be deleted, but after having triggered rules in xfst-kor.txt.
! The # symbol marks the end of the word string ( Root ... # )

! The mutations are here given tags and multi-char symbols.
! The symbols are deleted by DummyDeletion in xfst-kor.txt.

LEXICON mutation 
     :%^D  #        ; ! baseform
+Smut:%^S  #        ; ! soft, lenition
+Mmut:%^M  #        ; ! mixed
+Pmut:%^P  #        ; ! provexion
+Amut:%^A  #        ; ! aspiration

