! Cornish noun morphology

! Masculine stem classes
! ======================
! The nouns get tags (to the left of :, i.e. in the upper representation
! and the suffixes are found to the left of the colon
LEXICON m 
+N+Msc: sg ; ! no suffix, of course
+N+Msc: owpl ; ! no suffix yet, see LEXICON owpl below

LEXICON m-yow
+N+Msc:    sg ;
+N+Msc:    yowpl ;

LEXICON m-ys
+N+Msc:    sg ;
+N+Msc:    yspl ;

LEXICON m-yon
+N+Msc:    sg ;
+N+Msc:    yonpl ;

LEXICON m-aff-yon
+N+Msc:    sg ;
+N+Msc:    yonpl ;

LEXICON m-aff-i
+N+Msc:    sg  ;
+N+Msc:%^V ipl ; ! Trigger to signal MPh rule

LEXICON m-ag
+N+Msc:%^E  sg ; ! ditto
+N+Msc:     yonpl ;

LEXICON m-sgtantum
+N+Msc:  mutation ;

LEXICON m-pltantum
+N+Msc+PLT:   mutation ;

! Feminine stem classes
! =====================

LEXICON f
+N+Fem:  sg ;
+N+Fem:  yowpl ;

LEXICON faff
+N+Fem:    sg ;
+N+Fem:%^V yonpl ;

! Nest step: 
! Here we add suffixes. These may be common to several stem classes
! and are therefore pointed to in a different layer (this one)

! Number lexica
! =============

LEXICON sg
+Sg: mutation ; ! No suffix here, only tag.

LEXICON yonpl
+Pl:%^Vyon mutation ; ! Plural is yon, and there is mutation to come.

LEXICON owpl
+Pl:ow mutation ;

LEXICON yowpl
+Pl:yow mutation ;

LEXICON ipl 
+PL:i   mutation ;

LEXICON yspl
+PL:ys  mutation ;

! Mutation lexicon
! ================

! This is the final lexicon. Here, no suffix is added, only some marks.
! they will be deleted, but after having triggered rules in xfst-kor.txt.
! The # symbol marks the end of the word string ( Root ... # )

LEXICON mutation 
:%^D        #        ;
+S:%^S  #        ;
+M:%^M  #        ;
+P:%^P  #        ;
+A:%^A  #        ;
