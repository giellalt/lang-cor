! ==================== !
!! # Cornish prepositions 
! ==================== 

!!Thie file `stems/prepositions.lexc` lists prepositions and add tags.

!! Four classes:
!! * Class 1  
!! * Class 2 
!! * Class 3 
!! * Class 4 

LEXICON pp
!! **LEXICON @LEXNAME@**   tag only
+Pr: # ; !!≈ * @CODE@

!LEXICON pp-prona
!+Pr:          # ; ! bare form
!+Pr+1Sg:av    # ;
!+Pr+2Sg:as    # ;
!+Pr+Sem/Msc+3Sg:o # ;
!+Pr+Sem/Fem+3Sg:i # ;
!+Pr+1Pl:an    # ;
!+Pr+2Pl:owgh  # ;
!+Pr+3Pl:a     # ;


LEXICON pp-prono
!! **LEXICON @LEXNAME@** tag and person forms, -o-
+Pr:0          # ;  !!≈ * @CODE@ =  bare form
+Pr+1Sg:ov    # ;  !!≈ * @CODE@ 
+Pr+2Sg:os    # ; !!≈ * @CODE@
+Pr+Sem/Msc+3Sg:o # ; !! ...
+Pr+Sem/Fem+3Sg:i # ;
+Pr+1Pl:on    # ;
+Pr+2Pl:owgh  # ;
+Pr+3Pl:a     # ;

LEXICON pp-proni
!! **LEXICON @LEXNAME@** tag and person -i-
+Pr:          # ;  !!≈ * @CODE@ bare form
+Pr+1Sg:iv    # ;  !!≈ * @CODE@
+Pr+2Sg:is    # ;  !!≈ * @CODE@
+Pr+Sem/Msc+3Sg:o # ; !! ...
+Pr+Sem/Fem+3Sg:i # ;
+Pr+1Pl:yn    # ;
+Pr+2Pl:owgh  # ;
+Pr+3Pl:a     # ;

LEXICON pp-a
!! **LEXICON @LEXNAME@** tag and person form -a-
+Pr:          # ;  !!≈ * @CODE@ bare form
+Pr+1Sg:hanav    # ;  !!≈ * @CODE@
+Pr+2Sg:hanas   # ;  !!≈ * @CODE@
+Pr+Sem/Msc+3Sg:nodho # ; !! ---
+Pr+Sem/Fem+3Sg:nedhi # ;
+Pr+1Pl:hanan    # ;
+Pr+2Pl:hanowgh  # ;
+Pr+3Pl:nedha     # ;


LEXICON Preposition
!! **LEXICON @LEXNAME@** the list (some 20-30)
a          pp-a "of, from" ;  !!≈ * @CODE@
avel	   pp-prono "like" ; !!≈ * @CODE@
a-barth    pp "for the sake of, beside, in the name of, along with" ; !!≈ * @CODE@
a-berth	   pp "within" ; !! ...
a-der	   pp "without" ;
a-dhann    pp-prono "from under, from beneath" ;
a-dherag   pp-prono "before" ;
a-dhelergh pp "behind";
a-dhia	   pp "from, since" ;
a-dhywar   pp "down from" ;
a-dhyworth pp "from" ;
a-dryv 	   pp "behind" ; 
a-dro	   pp "around" ;
a-nans	   pp "below" ;
a-rag      pp-prono "before" ;
a-ugh	   pp-prono "above" ;       !alternation between <-gh> and <-h-> missing
bys		   pp "until, as far as" ;
dre: dredh pp-prono "through" ;	
dres	   pp-prono "over, beyond" ;
dhe        pp "to, for, at" ;
dhedh      pp-prono "to" ;
dherag     pp-prono "before" ;
dhywar	   pp "down from" ;
dhyworth   pp-proni "from" ;
drefen	   pp "because of" ;
erbynn     pp "against" ;
gans       pp "with, by" ;
heb		   pp "without" ;
herwydh	   pp "according to" ;
is		   pp "below, under" ;
orth       pp-proni "at, by" ;
rag        pp "for, to, in order to" ;
re		   pp "by (in oaths)";
ryb		   pp "beside" ;
tann       pp "by" ; 
war		   pp "on" ;
warbynn	   pp "against" ;
war-lergh  pp "behind" ;
wosa	   pp "after" ;
!yn : ynn     pp-prono "in" ;
yn        pp-prono "in" ;
yn-dann    pp-prono "under" ;
yntra : yntredh pp-prono "between" ;

