!! # Cornish noun morphology

!! ## Masculine stem classes
! ======================
!! The nouns get tags (to the left of :, i.e. in the upper representation
!! and the suffixes are found to the left of the colon

LEXICON m  !!≈ * **@CODE@** 
 +N+Msc: sg   ; ! no suffix, of course
 +N+Msc: owpl ; ! no suffix yet, see LEXICON owpl below

LEXICON m-yow  !!≈ * **@CODE@** 
 +N+Msc: sg    ;
 +N+Msc: yowpl ;

LEXICON m-ow  !!≈ * **@CODE@** 
 +N+Msc: sg   ;
 +N+Msc: owpl ;


LEXICON m-ys  !!≈ * **@CODE@** 
 +N+Msc: sg   ;
 +N+Msc: yspl ;

LEXICON m-es  !!≈ * **@CODE@** 
 +N+Msc: sg   ;
 +N+Msc: espl ;

LEXICON m-s  !!≈ * **@CODE@** 
 +N+Msc: sg  ;
 +N+Msc: spl ;

!LEXICON m-yn
! +N+Msc: sg   ;
! +N+Msc: ynpl ;

!LEXICON m-ens
! +N+Msc: sg    ;
! +N+Msc: enspl ;

LEXICON m-yon  !!≈ * **@CODE@** 
  +N+Msc: sg    ;
  +N+Msc: yonpl ;

!LEXICON m-aff-yon
! +N+Msc: sg    ;
! +N+Msc: yonpl ;

LEXICON m-aff-i  !!≈ * **@CODE@** 
 +N+Msc: sg  ;
 +N+Msc: ipl ; ! Need no trigger to signal MPh rule

LEXICON m-ag  !!≈ * **@CODE@** 
 +N+Msc:%^E sg    ; ! ditto
 +N+Msc:    yonpl ;

LEXICON m-sgtantum  !!≈ * **@CODE@** 
 +N+Msc: mutation ;

LEXICON m-pltantum  !!≈ * **@CODE@** 
 +N+Msc+PLT: mutation ;

!! ## Feminine stem classes
! =====================

LEXICON f  !!≈ * **@CODE@** 
 +N+Fem: sg   ;
 +N+Fem: owpl ;

LEXICON f-yow !!≈ * **@CODE@** 
 +N+Fem: sg    ;
 +N+Fem: yowpl ;

LEXICON f-edh !!≈ * **@CODE@** 
  +N+Fem:    sg    ;
  +N+Fem:    edhpl ;

!LEXICON faff
! +N+Fem:    sg    ;
! +N+Fem:%^V yonpl ;


!! Nest step:

!! Here we add suffixes. These may be common to several stem classes
!! and are therefore pointed to in a different layer (this one)


!! ## Number lexica
! =============

LEXICON sg !!≈ * **@CODE@** 
 +Sg: mutation ; ! No suffix here, only tag.

LEXICON yonpl !!≈ * **@CODE@** 
  +Pl:%>yon    mutation ; ! Plural is yon, and there is mutation to come.
! +Pl:%^V%>yon mutation ; ! Plural is yon, and there is mutation to come.

LEXICON owpl !!≈ * **@CODE@** 
  +Pl:%>ow  mutation ;
!    :>%>ek adjd     ;

LEXICON yowpl !!≈ * **@CODE@** 
 +Pl:%>yow mutation ;
    :%>yek adjd     ;

LEXICON ipl !!≈ * **@CODE@** 
 +Pl:%>i  mutation ;
    :%>ek adjd     ;

LEXICON yspl !!≈ * **@CODE@** 
 +Pl:%>ys mutation ;

!LEXICON ynpl
! +Pl:%>yn mutation ;

!LEXICON enspl
! +Pl:%>ens mutation ;
!    :%>ek  adjd     ;

LEXICON espl !!≈ * **@CODE@** 
 +Pl:%>es mutation ;
    :%>ek adjd     ;

LEXICON edhpl !!≈ * **@CODE@** 
 +Pl:%>edh mutation ;
    :%>ek  adjd     ;

LEXICON spl !!≈ * **@CODE@** 
 +Pl:%>s  mutation ;
    :%>ek adjd     ;

LEXICON adjd !!≈ * **@CODE@** 
 +A: # ;

!! ### Irregular plurals
!==================

LEXICON KI !!≈ * **@CODE@** 
 +Sg:ki   # ;
 +Pl:keun # ;

LEXICON DEN !!≈ * **@CODE@** 
 +Sg:den # ;
 +Pl:tus # ;

LEXICON OJYON !!≈ * **@CODE@** 
 +Sg:ojyon # ;
 +Pl:ohen  # ;

LEXICON GARLONT !!≈ * **@CODE@** 
 +N+Sg:           mutation ; ! or #?
 +N+Pl:%>%^STDELs mutation ; ! or #?

!! # Mutation lexicon
! ================

! This is the final lexicon. Here, no suffix is added, only some marks.
! they will be deleted, but after having triggered rules in xfst-kor.txt.
! The # symbol marks the end of the word string ( Root ... # )

! The mutations are here given tags and multi-char symbols.
! The symbols are deleted by DummyDeletion in xfst-kor.txt.

LEXICON mutation !!≈ * **@CODE@** 
      :%^D # ; ! baseform
 +Smut:%^S # ; ! soft, lenition
 +Mmut:%^M # ; ! mixed
 +Pmut:%^P # ; ! provexion
 +Amut:%^A # ; ! aspiration

